buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.researchgate:gradle-release:2.2.2"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1"
    }
}

apply plugin: "java"
apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "net.researchgate.release"
apply plugin: "com.jfrog.bintray"

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile {
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile("org.rundeck:rundeck-core:2.5.3")
    compile("org.slf4j:slf4j-api:1.7.12")

    pluginLibs("com.github.synyx:puppetdb-javaclient:0.1.6")
    pluginLibs("org.ehcache:ehcache:3.0.0.m3")
            {
                exclude( module: "slf4j-api")
            }
    testCompile("junit:junit:4.12")
    testCompile("org.hamcrest:hamcrest-core:1.3")
    testCompile("org.mockito:mockito-core:1.10.19")
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"

    def libList = configurations.pluginLibs.collect { "lib/" + it.name }.join(" ")

    manifest {
        attributes(
                "Rundeck-Plugin-Version": "1.1",
                "Rundeck-Plugin-File-Version": version,
                "Rundeck-Plugin-Archive": "true",
                "Rundeck-Plugin-Classnames": "org.synyx.rundeck.plugin.resources.puppetdb.PuppetDBResourceModelSourceFactory",
                "Rundeck-Plugin-Libs": "${libList}"
        )
    }
}
//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    configurations = ["archives"]
    pkg {
        repo = "rundeck-plugins"
        name = "rundeck-puppetdb-nodes-plugin"
        userOrg = "synyx"
        desc = "Provides PuppetDB nodes for Rundeck"
        websiteUrl = "https://github.com/synyx/rundeck-puppetdb-nodes-plugin"
        issueTrackerUrl = "https://github.com/synyx/rundeck-puppetdb-nodes-plugin/issues"
        vcsUrl = "https://github.com/synyx/rundeck-puppetdb-nodes-plugin.git"
        licenses = ["MIT"]
        labels = ["puppetdb", "rundeck"]
        publicDownloadNumbers = false
        version {
            name = $project.version
        }
    }
}