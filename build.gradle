apply plugin: "java"
apply plugin: "maven"

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile {
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile("org.rundeck:rundeck-core:2.5.3")
    compile("org.slf4j:slf4j-api:1.7.12")

    pluginLibs("com.puppetlabs.puppetdb:com.puppetlabs.puppetdb.javaclient:0.1.7-SNAPSHOT")
    pluginLibs("org.ehcache:ehcache:3.0.0.m3")
            {
                exclude( module: 'slf4j-api')
            }
    testCompile("junit:junit:4.12")
    testCompile("org.hamcrest:hamcrest-core:1.3")
    testCompile("org.mockito:mockito-core:1.10.19")
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"

    def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')

    manifest {
        attributes(
                "Rundeck-Plugin-Version": "1.1",
                "Rundeck-Plugin-File-Version": version,
                "Rundeck-Plugin-Archive": "true",
                "Rundeck-Plugin-Classnames": "org.synyx.rundeck.plugin.resources.puppetdb.PuppetDBResourceModelSourceFactory",
                'Rundeck-Plugin-Libs': "${libList}"
        )
    }
}
//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

